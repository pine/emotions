buildscript {
    ext {
        springBootVersion = '2.1.3.RELEASE'
        lombokVersion = '1.18.6'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE'
    }
}


def confFiles = fileTree("conf").include("**/*.yml")
confFiles.forEach { File src ->
    def dest = new File(src.absolutePath.replace(rootDir.absolutePath + "/conf", rootDir.absolutePath))
    dest.write("# This YAML source file was generated by the Gradle.\n\n")
    dest.append(src.readBytes())
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    group 'moe.pine.emotions'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        jcenter()
    }
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
        dependencies {
            dependency 'org.jetbrains:annotations:17.0.0'
            dependency 'javax.servlet:javax.servlet-api:4.0.1'
            dependency 'org.apache.xmlrpc:xmlrpc-client:3.1.3'
            dependency 'com.google.cloud:google-cloud-storage:1.49.0'

            // Guava
            dependency 'com.google.guava:guava:27.0.1-jre'

            // Apache Commons
            dependency 'commons-codec:commons-codec:1.11'
            dependency 'org.apache.commons:commons-lang3:3.8.1'
            dependency 'org.apache.commons:commons-collections4:4.3'

            // Test
            dependency 'junit:junit:4.12'
            dependency 'org.mockito:mockito-core:2.24.5'
        }
    }
    dependencies {
        // Lombok
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly  "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"

        // Logging
        implementation 'org.slf4j:slf4j-api'
        implementation 'ch.qos.logback:logback-classic'

        // Utilities
        implementation 'com.google.guava:guava'
        implementation 'org.apache.commons:commons-lang3'
        implementation 'org.apache.commons:commons-collections4'
        implementation 'org.jetbrains:annotations'

        // Test
        testImplementation 'junit:junit'
        testImplementation 'org.mockito:mockito-core'
    }
}
